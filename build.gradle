plugins {
    id "org.javamodularity.moduleplugin" version '1.8.12'
    id 'java'
    id 'java-library'
    id 'idea'
    id "org.sonarqube" version '4.0.0.2929'
    id 'jacoco'
    id "application"
    id "com.github.spotbugs" version '5.0.13'
}

spotbugs {
    reportLevel = 'high'
}

spotbugsMain {
    reports {
        html {
            stylesheet = 'fancy-hist.xsl'
        }
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "ak-git_SimpleApp"
        property "sonar.organization", "ak-git"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.branch.name", "master"
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/jacocoTestReport/jacocoTestReport.xml'
    }
}

jacoco {
    toolVersion = "0.8.8"
}

ext {
    test = 'org.junit:junit-bom:5.9.2'
    jsr305 = 'com.google.code.findbugs:jsr305:3.0.2'
    javaVersion = JavaVersion.VERSION_19
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = "8.0.1"
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

allprojects {
    group = 'com.ak'
    version = '2023.02.22'
    apply plugin: 'idea'
    idea {
        module {
            inheritOutputDirs = true
        }
    }

    sourceSets {
        main.output.resourcesDir = main.java.classesDirectory
    }

    apply plugin: 'java'
    apply plugin: 'java-library'
    apply plugin: 'org.javamodularity.moduleplugin'
    apply plugin: 'jacoco'

    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            url = 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        implementation jsr305
        testImplementation(platform(test))
        testImplementation('org.junit.jupiter:junit-jupiter')
    }

    test {
        forkEvery = 5
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
        maxHeapSize = '1G'
    }
}

application {
    mainModule = 'com.ak.app'
    mainClass = 'com.ak.app.MainApp'
}